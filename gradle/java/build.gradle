plugins {
    id 'java'
}

group = 'alvin.gradle'
version = "${System.getenv('CI_VERSION') ?: 1}.${System.getenv('CI_BUILD_NUM') ?: 0}"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

test {
    useJUnitPlatform()
}

repositories {
    maven {
        url 'http://maven.aliyun.com/nexus/content/groups/public/'
    }
    jcenter()
}

dependencies {
    implementation "com.google.guava:guava:27.0.1-jre"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0",
                       "org.junit.jupiter:junit-jupiter-engine:5.6.0",
                       "org.hamcrest:hamcrest-all:1.3"
}

task showProjectDir {
    doLast {
        println rootProject.projectDir
        println project.projectDir
        allprojects { println it}
    }
}

task showSourceSets {
    doLast {
        println sourceSets
        println sourceSets.main
        println sourceSets.main.java
        println sourceSets.main.resources
        println sourceSets.main.runtimeClasspath
        println sourceSets.main.output

        println sourceSets.test
        println sourceSets.test.java
        println sourceSets.test.resources
        println sourceSets.test.runtimeClasspath
        println sourceSets.test.output
    }
}

task fatJar(type: Jar) {
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

    manifest {
        attributes(
                'Implementation-Title': 'Gradle Demo',
                'Implementation-Version': version,
                'Main-Class': 'alvin.gradle.java.Main',
        )
    }

    archiveName "${project.group}.${project.name}-${project.version}-fat.jar"

    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Gradle Demo',
                'Implementation-Version': version,
                'Main-Class': 'alvin.gradle.java.Main',
                "Class-Path": makeClassPath()
        )
    }
    archiveName "${project.group}.${project.name}-${project.version}.jar"

    from "${buildDir}/libs/libs"
}

task copyJarsToLib {
    def toDir = "${buildDir}/libs/libs"
    doLast {
        copy {
            from configurations.compileClasspath
            into toDir
        }
    }
}

jar.dependsOn copyJarsToLib


task runMain(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'alvin.gradle.java.Main'
    args 'A', 'B', 'C'
}


/**
 * Add more java compile options
 */
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xdoclint:none', '-Xlint:none', '-nowarn']
}


/* methods */

def makeClassPath() {
    def jarNames = configurations.compileClasspath.collect {
        it.getName()
    }
    return '. libs/' + jarNames.join(' libs/')
}
